
model {

for(i in 1:n.nests){

  y[i] ~ dcat(omega[i,1:3]) #a categorical distribution is a multinomial distribution with an index of 1 (i.e., it is like rolling a die once and modeling the probabilities of getting a 1, 2, 3, 4, 5, or 6) 

  omega[i,1] <- (1-S[i])     #failure 
  omega[i,2] <- S[i]*(1-gamma[i])   #1 egg
  omega[i,3] <- S[i]*gamma[i]   #2 eggs 

  logit(S[i]) <- int.S + eps.S[year.rand[i]] + w.S[1]*beta.S.chla * chla.year.2[year[i]] + w.S[2]*beta.S.npgo * npgo.year.1[year[i]] + w.S[3]*beta.S.pdo * pdo.pre[year[i]] + w.S[4]*beta.S.sst * sst.pre[year[i]]
  logit(gamma[i]) <- int.gam + eps.gam[year.rand[i]] + w.gam[1]*beta.gam.chla * chla.year.1[year[i]] + w.gam[2]*beta.gam.npgo * npgo.pre[year[i]] + w.gam[3]*beta.gam.pdo * pdo.winter[year[i]] + w.gam[4]*beta.gam.sst * sst.year.1[year[i]]

} 

for(z in 1:n.years){  #random effects to explain residual annual variation 
  eps.S[z] ~ dnorm(0,tau.S) 
  eps.gam[z] ~ dnorm(0,tau.gam)
} 

for(i in 1:4){
  w.S[i] ~ dbern(0.5)
  w.gam[i] ~ dbern(0.5)
}

tau.total ~ dgamma(3.29,7.8)
tau.model.S <- tau.total/K.S
K.S <- w.S[1] + w.S[2] + w.S[3] + w.S[4] + 1
tau.model.gam <- tau.total/K.gam
K.gam <- w.gam[1] + w.gam[2] + w.gam[3] + w.gam[4] + 1


####################################
### Addressing missing chla data ###

l.est.chla[1] ~ dnorm(int.chla, tau.chla) #estimated first value of chla dataset
#loop over remaining months/years
for (q in 2:total.chla){
  l.est.chla[q] ~ dnorm(l.est.chla[q-1], tau.chla)
}

# address first time point
for(j in 1:total.chla){
  real.chla[j] <- exp(l.est.chla[j]) #exponentiating est.chla value; expected value of chla
}

# full-year; May(t-1):Apr(t)
for(p in 1:n.years.new){
  chla.year.1[p] <- mean(real.chla[((p*12)-11) : (p*12)])
}

# full-year; Oct(t-1):Sep(t)
for(p in 1:n.years.new){
  chla.year.2[p] <- mean(real.chla[((p*12)-6) : ((p*12)+5)])
}

### Addressing missing SST data ### 

#loop over remaining months/years
for (z in 2:total.sst){
  l.sst.data[z] ~ dnorm(l.sst.data[z-1], tau.sst)
}

# address first time point
for(k in 1:total.sst){
  real.sst[k] <- exp(l.sst.data[k]) #exponentiating est.sst value; expected value of sst
}

# full-year; May(t-1):Apr(t)
for(p in 1:n.years.new){
  sst.year.1[p] <- mean(real.sst[((p*12)-11) : (p*12)])
}

## pre-breed; Jan(t):Apr(t) ##
for(p in 1:n.years.new){
  sst.pre[p] <- mean(real.sst[((p*12)-3) : (p*12)])
}

##############
### Priors ###

int.chla ~ dnorm(0,1)
tau.S <- pow(sigma.S,-2)
sigma.S ~ dunif(0,10) 
tau.gam <- pow(sigma.gam,-2)
sigma.gam ~ dunif(0,10)
tau.chla <- pow(sigma.chla,-2)
sigma.chla ~ dunif(0,30)
beta.S.chla ~ dnorm(0,tau.model.S)
beta.gam.chla ~ dnorm(0,tau.model.gam)
beta.S.npgo ~ dnorm(0,tau.model.S)
beta.gam.npgo ~ dnorm(0,tau.model.gam)
beta.S.pdo ~ dnorm(0,tau.model.S)
beta.gam.pdo ~ dnorm(0,tau.model.gam)
int.sst ~ dnorm(0,1)
tau.sst <- pow(sigma.sst,-2)
sigma.sst ~ dunif(0,30)
beta.S.sst ~ dnorm(0,tau.model.S)
beta.gam.sst ~ dnorm(0,tau.model.gam)

int.S ~ dnorm(0,1) 
int.gam ~ dnorm(0,1)

mean.S <- 1/(1+exp(-(int.S)))
mean.gam <- 1/(1+exp(-(int.gam)))

}
